# 分析阴影渲染【away3d】

![](http://www.feng3d.me/wordpress/wp-content/uploads/2015/07/20150704185636.png)

对应的away3d测试代码（away3d 4.1.6）

```
package
{
	import flash.display.Sprite;
	import flash.display.StageAlign;
	import flash.display.StageScaleMode;
	import flash.events.Event;
	
	import away3d.cameras.Camera3D;
	import away3d.containers.ObjectContainer3D;
	import away3d.containers.Scene3D;
	import away3d.containers.View3D;
	import away3d.controllers.LookAtController;
	import away3d.core.base.Geometry;
	import away3d.debug.AwayStats;
	import away3d.entities.Mesh;
	import away3d.lights.DirectionalLight;
	import away3d.lights.shadowmaps.NearDirectionalShadowMapper;
	import away3d.materials.TextureMaterial;
	import away3d.materials.lightpickers.StaticLightPicker;
	import away3d.materials.methods.FilteredShadowMapMethod;
	import away3d.materials.methods.NearShadowMapMethod;
	import away3d.primitives.PlaneGeometry;
	import away3d.primitives.SphereGeometry;
	import away3d.utils.Cast;
 
	/**
	 * 测试阴影
	 */
	[SWF(backgroundColor = "#000000", frameRate = "30")]
	public class ShadowTest extends Sprite
	{
		//floor diffuse map
		[Embed(source = "/../embeds/rockbase_diffuse.jpg")]
		private var FloorDiffuse:Class;
 
//		//floor normal map
//		[Embed(source = "/../embeds/rockbase_normals.png")]
//		private var FloorNormals:Class;
//
//		//floor specular map
//		[Embed(source = "/../embeds/rockbase_specular.png")]
//		private var FloorSpecular:Class;
 
		//body diffuse map
		[Embed(source = "/../embeds/hellknight/hellknight_diffuse.jpg")]
		private var BodyDiffuse:Class;
 
//		//body normal map
//		[Embed(source = "/../embeds/hellknight/hellknight_normals.png")]
//		private var BodyNormals:Class;
//
//		//bidy specular map
//		[Embed(source = "/../embeds/hellknight/hellknight_specular.png")]
//		private var BodySpecular:Class;
 
		//engine variables
		private var scene:Scene3D;
		private var camera:Camera3D;
		private var view:View3D;
		private var cameraController:LookAtController;
		private var awayStats:AwayStats;
 
		//light objects
		private var whiteLight:DirectionalLight;
		private var lightPicker:StaticLightPicker;
		private var shadowMapMethod:NearShadowMapMethod;
		private var count:Number = 0;
 
		//material objects
		private var groundMaterial:TextureMaterial;
		private var bodyMaterial:TextureMaterial;
 
		//scene objects
		private var placeHolder:ObjectContainer3D;
		private var mesh:Mesh;
		private var ground:Mesh;
 
		/**
		 * Constructor
		 */
		public function ShadowTest()
		{
			init();
		}
 
		/**
		 * Global initialise function
		 */
		private function init():void
		{
			initEngine();
			initLights();
			initMaterials();
			initObjects();
			initListeners();
		}
 
		/**
		 * Initialise the engine
		 */
		private function initEngine():void
		{
			stage.scaleMode = StageScaleMode.NO_SCALE;
			stage.align = StageAlign.TOP_LEFT;
 
			view = new View3D();
			scene = view.scene;
			camera = view.camera;
 
			camera.lens.far = 5000;
			camera.z = -200;
			camera.y = 160;
 
			//setup controller to be used on the camera
			placeHolder = new ObjectContainer3D();
			placeHolder.y = 50;
			cameraController = new LookAtController(camera, placeHolder);
 
			view.addSourceURL("srcview/index.html");
			addChild(view);
 
			awayStats = new AwayStats(view);
			addChild(awayStats);
		}
 
		/**
		 * Initialise the lights
		 */
		private function initLights():void
		{
			whiteLight = new DirectionalLight(-50, -20, 10);
			whiteLight.color = 0xffffee;
			whiteLight.castsShadows = true;
			whiteLight.ambient = 1;
			whiteLight.ambientColor = 0x303040;
			whiteLight.shadowMapper = new NearDirectionalShadowMapper(0.2);
			scene.addChild(whiteLight);
 
			lightPicker = new StaticLightPicker([whiteLight]);
 
 
			//create a global shadow method
			shadowMapMethod = new NearShadowMapMethod(new FilteredShadowMapMethod(whiteLight),0.1);
			shadowMapMethod.epsilon = 0.1;
		}
 
		/**
		 * Initialise the materials
		 */
		private function initMaterials():void
		{
			//ground material
			groundMaterial = new TextureMaterial(Cast.bitmapTexture(FloorDiffuse));
			groundMaterial.smooth = true;
			groundMaterial.repeat = true;
			groundMaterial.mipmap = true;
			groundMaterial.lightPicker = lightPicker;
//			groundMaterial.normalMap = Cast.bitmapTexture(FloorNormals);
//			groundMaterial.specularMap = Cast.bitmapTexture(FloorSpecular);
			groundMaterial.shadowMethod = shadowMapMethod;
 
			//body material
			bodyMaterial = new TextureMaterial(Cast.bitmapTexture(BodyDiffuse));
			bodyMaterial.specular = 1.5;
//			bodyMaterial.specularMap = Cast.bitmapTexture(BodySpecular);
//			bodyMaterial.normalMap = Cast.bitmapTexture(BodyNormals);
			bodyMaterial.lightPicker = lightPicker;
			bodyMaterial.shadowMethod = shadowMapMethod;
		}
 
		/**
		 * Initialise the scene objects
		 */
		private function initObjects():void
		{
			initMesh();
 
			//create a snowy ground plane
			ground = new Mesh(new PlaneGeometry(50000, 50000, 1, 1), groundMaterial);
			ground.geometry.scaleUV(200, 200);
			ground.castsShadows = false;
			scene.addChild(ground);
		}
 
		/**
		 * Initialise the hellknight mesh
		 */
		private function initMesh():void
		{
			//grab mesh object and assign our material object
			
			var sphereGeometry:Geometry = new SphereGeometry();
			
			mesh = new Mesh(sphereGeometry,bodyMaterial);
			mesh.castsShadows = true;
			mesh.y = 50;
			mesh.z = 200;
			
			scene.addChild(mesh);
			
			//add our lookat object to the mesh
			mesh.addChild(placeHolder);
		}
 
		/**
		 * Initialise the listeners
		 */
		private function initListeners():void
		{
			addEventListener(Event.ENTER_FRAME, onEnterFrame);
			stage.addEventListener(Event.RESIZE, onResize);
			onResize();
		}
 
		/**
		 * Navigation and render loop
		 */
		private function onEnterFrame(event:Event):void
		{
			cameraController.update();
 
			//update character animation
			if (mesh)
				mesh.rotationY += 1;
 
			count += 0.01;
 
			view.render();
		}
 
		/**
		 * stage listener for resize events
		 */
		private function onResize(event:Event = null):void
		{
			view.width = stage.stageWidth;
			view.height = stage.stageHeight;
			awayStats.x = stage.stageWidth - awayStats.width;
		}
	}
}
```

agal代码分析

```
Compiling AGAL Code:
---------- 顶点渲染程序 ----------
mov vt0, va0							//va0:模型顶点坐标
mov vt1, va1							//va1：顶点法线
mov v1,va2								//
m44 vt2, vt0, vc4						//计算顶点全局（世界）坐标				vc4:模型到世界 转换矩阵
m44 vt5, vt2, vc0						//计算顶点投影坐标						vc0：投影矩阵
mov v0, vt5								//顶点投影坐标传递到 片段寄存器			v0:顶点投影坐标
mov op, vt5								//输出坐标
m33 v2.xyz, vt1, vc8					//计算法线								vc8:法线 从模型到世界 转换矩阵
mov v2.w, vt1.w							//计算法线								v2：法线
sub v3, vc12, vt2						//计算视线	摄像机坐标-顶点全局坐标		v3：视线
 
m44 vt1, vt2, vc14						//计算顶点深度值						vc14:深度映射投影矩阵
div vt1, vt1, vt1.w						//计算顶点深度值(x,y计算深度图中的uv，z计算深度)
mul vt1.xy, vt1.xy, vc13.xy				//x,y计算深度图中的uv
add v4, vt1, vc13.xxwz					//计算深度图坐标						v4：深度图坐标
 
 
---------- 片段渲染程序 ----------
nrm ft0.xyz, v2							//标准化法线							ft0：法线
mov ft0.w, v2.w							//
nrm ft1.xyz, v3							//标准化视线							ft1：视线
mov ft1.w,   v3.w 						//
 
dp3 ft3.x, fc2, ft0						//计算漫反射
max ft3.w, ft3.x, fc0.y
mul ft3.w, ft3.w, fc2.w
mul ft3, ft3.w, fc3						//ft3:漫反射颜色值
 
add ft4, fc2, ft1						//计算方向光镜面反射
nrm ft4.xyz, ft4
dp3 ft4.w, ft0, ft4
sat ft4.w, ft4.w
pow ft4.w, ft4.w, fc1.w
mul ft4.w, ft4.w, fc2.w
mul ft4.xyz, fc4, ft4.w					//ft4：镜面反射颜色值
 
mov ft2, fc5							//计算环境光
 
//---------------------- 开始计算阴影 ------------------------//
 
mov ft6, v4								//计算阴影								v4：深度图坐标
tex ft5, v4, fs0 <2d, nearest, clamp>	//读取阴影图值							fs0:阴影图纹理
dp4 ft5.z, ft5, fc6						//										fc6:(1,1/255,1/255/255,1/255/255/255)
slt ft6.z, v4.z, ft5.z					//比较深度，ft6.z==0为阴影				此处指为0或者1
 
add ft6.x, v4.x, fc8.z					//取(1,0)位置的深度值					fc8(0.5,2048,1/2048,0)
tex ft5, ft6, fs0 <2d, nearest, clamp>
dp4 ft5.z, ft5, fc6
slt ft6.w, v4.z, ft5.z					//与(1,0)深度做比较
 
mul ft5.x, v4.x, fc8.y					//计算顶点在阴影图的像素x位置
frc ft5.x, ft5.x						//取出分数部分  0<分数部分<1
sub ft6.w, ft6.w, ft6.z					//根据X方向分数部分来混合(0,0)与(1,0)的深度结果  	定义：ft6.z = x0,ft6.w = x1;	=> ft6.w = x1-x0
mul ft6.w, ft6.w, ft5.x					//												定义：ft5.x = d;				=> ft6.w = (x1-x0) * d
add ft0.w, ft6.z, ft6.w					//ft0.w保存阴影值   							混合方程						=> ft0.w = x0 + (x1-x0) * d;有d=0时，ft0.w = x0;d = 1时ft0.w = x1;
 
mov ft6.x, v4.x							//与(0,1)深度做比较
add ft6.y, v4.y, fc8.z
tex ft5, ft6, fs0 <2d, nearest, clamp>
dp4 ft5.z, ft5, fc6
slt ft6.z, v4.z, ft5.z
 
add ft6.x, v4.x, fc8.z					//与(1,1)深度做比较
tex ft5, ft6, fs0 <2d, nearest, clamp>
dp4 ft5.z, ft5, fc6
slt ft6.w, v4.z, ft5.z
 
mul ft5.x, v4.x, fc8.y					//根据X方向分数部分来混合(0,1)与(1,1)的深度结果  x1-x0
frc ft5.x, ft5.x
sub ft6.w, ft6.w, ft6.z
mul ft6.w, ft6.w, ft5.x
add ft6.w, ft6.z, ft6.w
 
mul ft5.x, v4.y, fc8.y					//再次根据Y方向分数部分来混合两次X方向混合的结果
frc ft5.x, ft5.x
sub ft6.w, ft6.w, ft0.w
mul ft6.w, ft6.w, ft5.x
add ft0.w, ft0.w, ft6.w					//ft0.w保存阴影值  此处值为(0,1)区间内   0为阴影
 
add ft0.w, ft0.w, fc7.y					//添加(1-阴影透明度)
sat ft0.w, ft0.w						//使阴影值在(0,1)区间内
 
//根据阴影离摄像机的距离计算阴影的透明度
abs ft5.x, v0.w							//获取顶点深度正值
sub ft5.x, ft5.x, fc9.x					//深度-最近可观察阴影距离
mul ft5.x, ft5.x, fc9.y					//计算衰减值
sat ft5.x, ft5.x						//
sub ft5.x, fc9.w,ft5.x					//ft5.x（阴影透明度）=1-衰减值
 
sub ft0.w, fc9.w,ft0.w					//ft0.w==1时为阴影
mul ft0.w, ft0.w, ft5.x					//阴影乘以透明度
sub ft0.w, fc9.w,ft0.w					//ft0.w==0时为阴影
 
//---------------------- 结束计算阴影 ------------------------//
 
mul ft3.xyz, ft3, ft0.w								//使用阴影到漫反射   阴影叠加到漫反射因子上
tex ft5, v1, fs1 <2d,linear,miplinear,clamp>		//fs1:贴图
sat ft3, ft3
add ft2.xyz, ft3, ft2								//漫反射 + 环境光 因子
mul ft2.xyz, ft5, ft2								//计算漫反射与环境光造成的颜色
mov ft2.w, ft5.w
 
mul ft4.xyz, ft4, ft0.w								//使用阴影到镜面反射
mul ft4.xyz, ft4, fc1
add ft2.xyz, ft2, ft4								//添加镜面反射光到总颜色
 
mov oc, ft2											//输出最终颜色值
```

对应的输入gpu的常量数据分析

```
顶点常量数据
"_vertexConstantData"	Vector.<Number> (@66d9539)	
vc0：投影矩阵
	[0]	1.299038052558899		
	[1]	0	
	[2]	0	
	[3]	0	
	[4]	0	
	[5]	1.7128880023956299	
	[6]	0.25693318247795105	
	[7]	-222.67543029785156	
	[8]	0	
	[9]	-0.1489361971616745	
	[10]	0.9929080009460449	
	[11]	202.3310546875	
	[12]	0	
	[13]	-0.14834044873714447	
	[14]	0.9889363646507263	
	[15]	221.52174377441406	
 
vc4:模型到世界 转换矩阵
	[16]	1	
	[17]	0	
	[18]	0	
	[19]	0	
	[20]	0	
	[21]	1	
	[22]	0	
	[23]	0	
	[24]	0	
	[25]	0	
	[26]	1	
	[27]	0	
	[28]	0	
	[29]	0	
	[30]	0	
	[31]	1	
 
vc8:法线 从模型到世界 转换矩阵   此时有vc4 == vc8
	[32]	1	
	[33]	0	
	[34]	0	
	[35]	0	
	[36]	0	
	[37]	1	
	[38]	0	
	[39]	0	
	[40]	0	
	[41]	0	
	[42]	1	
	[43]	0	
	[44]	0	
	[45]	0	
	[46]	0	
	[47]	1	
 
vc12:		摄像机坐标
	[48]	0	
	[49]	160 [0xa0]	
	[50]	-200 [0xffffff38]	
	[51]	1	
 
vc13:		(-1,1)->(0,1) 区间映射使用
	[52]	0.5	
	[53]	-0.5	
	[54]	0	
	[55]	-9.017422844679069E-4	//此处未使用
 
vc14:深度映射投影矩阵
	[56]	2.7857397799380124E-4	
	[57]	-1.9902504991287095E-11	
	[58]	0.001392870326526463	
	[59]	-0.6773166060447693	
	[60]	-3.996176819782704E-4	
	[61]	0.0010390059323981404	
	[62]	7.992352038854733E-5	
	[63]	-0.13299256563186646	
	[64]	-8.23174268589355E-5	
	[65]	-3.2926971471169963E-5	
	[66]	1.6463482097606175E-5	
	[67]	0.905006468296051	
	[68]	0	
	[69]	0	
	[70]	0	
	[71]	1	
	
	fixed	false	
	length	72 [0x48]	
 
	
片段常量数据
"_fragmentConstantData"	Vector.<Number> (@66d9561)	
fc0:					常用数据
	[0]	0.5	
	[1]	0	
	[2]	0.00392156862745098			1/255
	[3]	1	
fc1:					材质镜面反射光数据
	[4]	1	
	[5]	1	
	[6]	1	
	[7]	50 [0x32]		高光值
fc2:					灯光方向
	[8]	0.91287094159291	
	[9]	0.36514836471623513	
	[10]	-0.18257413765463407	
	[11]	1	
fc3:					漫反射光因子
	[12]	1	
	[13]	1	
	[14]	0.9333333333333333	
	[15]	1	
fc4:					镜面反射光因子
	[16]	1	
	[17]	1	
	[18]	0.9333333333333333	
	[19]	1	
fc5:					环境光因子
	[20]	0.18823529411764706	
	[21]	0.18823529411764706	
	[22]	0.25098039215686274	
	[23]	1	
fc6:	颜色值转换为深度向量		
	[24]	1								1
	[25]	0.00392156862745098				1/255
	[26]	1.5378700499807768E-5			1/255/255
	[27]	6.030862941101084E-8			1/255/255/255
fc7:
	[28]	0	
	[29]	0								//用于计算阴影透明度(1-阴影透明度)
	[30]	0	
	[31]	1	
fc8:
	[32]	0.5											//
	[33]	2048 [0x800]								//阴影图尺寸
	[34]	4.8828125E-4					1/2048		//
	[35]	0	
fc9:
	[36]	916.4000000000001				//观察到阴影的最近距离
	[37]	0.01004016064257029				//阴影值/距离	=> 物体离摄像机的距离越远，阴影透明度越低
	[38]	0	
	[39]	1	
 
	fixed	false	
	length	40 [0x28]
```
