# gpu渲染md5动画 渲染程序详解

![](http://www.feng3d.me/wordpress/wp-content/uploads/2014/07/20140717212557-300x224.jpg)

[查看实例](http://www.feng3d.me/feng3dDemo/SkeletonAnimatorTest.html)

感谢潘哥的提供给我易懂的源码与简单md5模型让我更轻松地去理解md5解析与渲染全过程！

[解析MD5MESH模型文件](blogs/2014/07/10/1.md)与[解析md5anim动作文件](blogs/2014/07/17/1.md)前面已经有介绍了，这里主要讲解下使用gpu对md5动画蒙皮过程。

渲染代码见(away3d)SkeletonAnimationSet.getAGALVertexCode()

```
   /**
    * @inheritDoc
    */
public function getAGALVertexCode(pass:MaterialPassBase, sourceRegisters:Vector.<String>, targetRegisters:Vector.<String>, profile:String):String
{
    var len:uint = sourceRegisters.length;
    
    var indexOffset0:uint = pass.numUsedVertexConstants;
    var indexOffset1:uint = indexOffset0 + 1;
    var indexOffset2:uint = indexOffset0 + 2;
    var indexStream:String = "va" + pass.numUsedStreams;
    var weightStream:String = "va" + (pass.numUsedStreams + 1);
    var indices:Array = [ indexStream + ".x", indexStream + ".y", indexStream + ".z", indexStream + ".w" ];
    var weights:Array = [ weightStream + ".x", weightStream + ".y", weightStream + ".z", weightStream + ".w" ];
    var temp1:String = findTempReg(targetRegisters);
    var temp2:String = findTempReg(targetRegisters, temp1);
    var dot:String = "dp4";
    var code:String = "";
    
    for (var i:uint = 0; i < len; ++i) {
        
        var src:String = sourceRegisters[i];
        
        for (var j:uint = 0; j < _jointsPerVertex; ++j) {
            code += dot + " " + temp1 + ".x, " + src + ", vc[" + indices[j] + "+" + indexOffset0 + "]		\n" +
                dot + " " + temp1 + ".y, " + src + ", vc[" + indices[j] + "+" + indexOffset1 + "]    	\n" +
                dot + " " + temp1 + ".z, " + src + ", vc[" + indices[j] + "+" + indexOffset2 + "]		\n" +
                "mov " + temp1 + ".w, " + src + ".w		\n" +
                "mul " + temp1 + ", " + temp1 + ", " + weights[j] + "\n"; // apply weight
            
            // add or mov to target. Need to write to a temp reg first, because an output can be a target
            if (j == 0)
                code += "mov " + temp2 + ", " + temp1 + "\n";
            else
                code += "add " + temp2 + ", " + temp2 + ", " + temp1 + "\n";
        }
        // switch to dp3 once positions have been transformed, from now on, it should only be vectors instead of points
        dot = "dp3";
        code += "mov " + targetRegisters[i] + ", " + temp2 + "\n";
    }
    
    return code;
}
```

away3d的代码我不做解释了。

渲染代码见(feng3d)SkeletonAnimationSet.getAGALVertexCode()

```
override public function getAGALVertexCode():String
{
    //获取 全局变换矩阵 起始位置
    var startIndex:int = reg(globalmatricesReg, numJoints * 3).index;

    return "dp4 vt1.x, " + reg(positionReg) + ", vc[" + reg(jointindexReg) + ".x+" + startIndex + "] \n" + //		计算该顶点坐标通关该关节得到的x值-----------1
        "dp4 vt1.y, " + reg(positionReg) + ", vc[" + reg(jointindexReg) + ".x+" + (startIndex + 1) + "]    	\n" + //计算该顶点坐标通关该关节得到的y值-----------2
        "dp4 vt1.z, " + reg(positionReg) + ", vc[" + reg(jointindexReg) + ".x+" + (startIndex + 2) + "]		\n" + //计算该顶点坐标通关该关节得到的z值-----------3
        "mov vt1.w, " + reg(positionReg) + ".w		\n" + // w值不变-----------------4
        "mul vt1, vt1, " + reg(JointWeightsReg) + ".x\n" + // 通过权重计算该关节对顶点的影响值---------------5
        "mov vt2, vt1\n" + //vt2保存了计算后的顶点坐标，第一个关节影响值使用mov赋值，后面的关节将会使用add来累加-----------------6(1到6将会对每个与该顶点相关的关节调用，该实例中只有一个关节，所以少了个for循环)
        "mov " + reg(p) + ", vt2\n"; // 赋值给顶点坐标寄存器，提供给后面投影使用
}
```

positionReg：顶点位置寄存器

jointindexReg：关节索引数据寄存器

JointWeightsReg：关节权重数据寄存器

globalmatricesReg：关节全局变换矩阵静态数据

1到3相当于顶点坐标乘以骨骼全局变换矩阵静态数据，为了节省一个寄存器（程序中使用了3个向量，如果使用矩阵保存的话消耗4个向量）。

 

开始的md5动画实例中最终渲染代码如下：

```
dp4 vt1.x, va1, vc[va2.x+4] 
dp4 vt1.y, va1, vc[va2.x+5]    	
dp4 vt1.z, va1, vc[va2.x+6]		
mov vt1.w, va1.w		
mul vt1, vt1, va3.x
mov vt2, vt1
mov vt0, vt2
m44 op, vt0, vc0
mov v0,va0
 
--------------------
mov ft0, fc0
tex ft0, v0, fs0 <2d,linear,miplinear,repeat>
mov oc, ft0
```

vt0、vt1、vt2：临时变量

va1:顶点坐标数据

vc:前面4个保存的是投影矩阵，后面n*3保存的是n个关节的全局变换矩阵

v0(v0、v1、v2、v3):投影矩阵

vc[va2.x+4]、vc[va2.x+5] 、vc[va2.x+6]合起来表示一个关节全局变换矩阵（只能从4开始，前面0到3被投影矩阵占用）

va2.x:关节编号（va2.x/3）

va0:uv坐标

va2.x表示与顶点关联的第一个关节，va2.yzw表示第234个关节，所以只能4个。不过以后可以考虑使用两个顶点寄存器比如va2与va3，这样就可以使用gpu蒙皮away3d给出的实例了。

从m44 op, vt0, vc0开始就是普通的投影，片段渲染的代码了,不做解释了。

只有这个模型可以正常使用gpu渲染，away3d例子中的模型由于顶点关联的关节数过多（不能超过4）无法使用gpu渲染而直接使用cpu蒙皮了（away3d中也是这样）。没有顶点与关节关联数超出1个的模型可用，蒙皮渲染代码暂时写到这了。