# 分析away3d光照AGAL代码

## 创建简单实例

首先获取一个简单的实例，使用away3d官方实例Basic_Shading修改得到相对简单的实例TestDirectionalLight，只用一个光源，物体也只留一个，纹理也只用一个。

![](http://www.feng3d.me/wordpress/wp-content/uploads/2014/09/20140916103435-300x251.jpg)

```
package
{
	import flash.display.Sprite;
	import flash.display.StageAlign;
	import flash.display.StageScaleMode;
	import flash.events.Event;
	import flash.events.MouseEvent;
	import flash.geom.Vector3D;
	
	import away3d.cameras.Camera3D;
	import away3d.containers.Scene3D;
	import away3d.containers.View3D;
	import away3d.controllers.HoverController;
	import away3d.debug.AwayStats;
	import away3d.entities.Mesh;
	import away3d.lights.DirectionalLight;
	import away3d.materials.TextureMaterial;
	import away3d.materials.lightpickers.StaticLightPicker;
	import away3d.primitives.PlaneGeometry;
	import away3d.utils.Cast;
	
	/**
	 * 测试方向光
	 * @author warden_feng
	 */	
	[SWF(backgroundColor="#000000", frameRate="60", quality="LOW")]
	public class TestDirectionalLight extends Sprite
	{
		//plane textures
		[Embed(source="/../embeds/floor_diffuse.jpg")]
		public static var FloorDiffuse:Class;
//		[Embed(source="/../embeds/floor_normal.jpg")]
//		public static var FloorNormals:Class;
    	
    	//engine variables
    	private var scene:Scene3D;
		private var camera:Camera3D;
		private var view:View3D;
		private var cameraController:HoverController;
		
		//material objects
		private var planeMaterial:TextureMaterial;
		
		//light objects
//		private var light1:DirectionalLight;
		private var light2:DirectionalLight;
		private var lightPicker:StaticLightPicker;
		
		//scene objects
		private var plane:Mesh;
		
		//navigation variables
		private var move:Boolean = false;
		private var lastPanAngle:Number;
		private var lastTiltAngle:Number;
		private var lastMouseX:Number;
		private var lastMouseY:Number;
		
		/**
		 * Constructor
		 */
		public function TestDirectionalLight()
		{
			init();
		}
		
		/**
		 * Global initialise function
		 */
		private function init():void
		{
			initEngine();
			initLights();
			initMaterials();
			initObjects();
			initListeners();
		}
		
		/**
		 * Initialise the engine
		 */
		private function initEngine():void
		{
			stage.scaleMode = StageScaleMode.NO_SCALE;
			stage.align = StageAlign.TOP_LEFT;
			
			scene = new Scene3D();
			
			camera = new Camera3D();
			
			view = new View3D();
			view.antiAlias = 4;
			view.scene = scene;
			view.camera = camera;
			
			//setup controller to be used on the camera
			cameraController = new HoverController(camera);
			cameraController.distance = 1000;
			cameraController.minTiltAngle = 0;
			cameraController.maxTiltAngle = 90;
			cameraController.panAngle = 45;
			cameraController.tiltAngle = 20;
			
			view.addSourceURL("srcview/index.html");
			addChild(view);
			
			addChild(new AwayStats(view));
		}
		
		/**
		 * Initialise the materials
		 */
		private function initMaterials():void
		{
			planeMaterial = new TextureMaterial(Cast.bitmapTexture(FloorDiffuse));
//			planeMaterial.normalMap = Cast.bitmapTexture(FloorNormals);
			planeMaterial.lightPicker = lightPicker;
			planeMaterial.repeat = true;
			planeMaterial.mipmap = false;
		}
		
		/**
		 * Initialise the lights
		 */
		private function initLights():void
		{
//			light1 = new DirectionalLight();
//			light1.direction = new Vector3D(0, -1, 0);
//			light1.ambient = 0.1;
//			light1.diffuse = 0.7;
//			
//			scene.addChild(light1);
			
			light2 = new DirectionalLight();
			light2.direction = new Vector3D(1, -1, 1);
			light2.color = 0x00FFFF;
			light2.ambient = 0.1;
			light2.diffuse = 0.7;
			
			scene.addChild(light2);
			
//			lightPicker = new StaticLightPicker([light1, light2]);
			lightPicker = new StaticLightPicker([light2]);
		}
		
		/**
		 * Initialise the scene objects
		 */
		private function initObjects():void
		{
			plane = new Mesh(new PlaneGeometry(1000, 1000), planeMaterial);
			plane.geometry.scaleUV(2, 2);
			plane.y = -20;
			
			scene.addChild(plane);
		}
		
		/**
		 * Initialise the listeners
		 */
		private function initListeners():void
		{
			addEventListener(Event.ENTER_FRAME, onEnterFrame);
			stage.addEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
			stage.addEventListener(MouseEvent.MOUSE_UP, onMouseUp);
			stage.addEventListener(Event.RESIZE, onResize);
			onResize();
		}
		
		/**
		 * Navigation and render loop
		 */
		private function onEnterFrame(event:Event):void
		{
			if (move) {
				cameraController.panAngle = 0.3*(stage.mouseX - lastMouseX) + lastPanAngle;
				cameraController.tiltAngle = 0.3*(stage.mouseY - lastMouseY) + lastTiltAngle;
			}
			
//			light1.direction = new Vector3D(Math.sin(getTimer()/10000)*150000, 1000, Math.cos(getTimer()/10000)*150000);
			
			view.render();
		}
		
		/**
		 * Mouse down listener for navigation
		 */
		private function onMouseDown(event:MouseEvent):void
		{
			lastPanAngle = cameraController.panAngle;
			lastTiltAngle = cameraController.tiltAngle;
			lastMouseX = stage.mouseX;
			lastMouseY = stage.mouseY;
			move = true;
			stage.addEventListener(Event.MOUSE_LEAVE, onStageMouseLeave);
		}
		
		/**
		 * Mouse up listener for navigation
		 */
		private function onMouseUp(event:MouseEvent):void
		{
			move = false;
			stage.removeEventListener(Event.MOUSE_LEAVE, onStageMouseLeave);
		}
		
		/**
		 * Mouse stage leave listener for navigation
		 */
		private function onStageMouseLeave(event:Event):void
		{
			move = false;
			stage.removeEventListener(Event.MOUSE_LEAVE, onStageMouseLeave);
		}
		
		/**
		 * stage listener for resize events
		 */
		private function onResize(event:Event = null):void
		{
			view.width = stage.stageWidth;
			view.height = stage.stageHeight;
		}
	}
}
```

## 获取AGAL代码

flash builder控制台中得到的AGAL代码

```
Compiling AGAL Code:
--------------------
mov vt0, va0
mov vt1, va1
mov v0,va2
m44 vt2, vt0, vc4
m44 op, vt2, vc0		
m33 v1.xyz, vt1, vc8
mov v1.w, vt1.w	
sub v2, vc12, vt2
 
--------------------
nrm ft0.xyz, v1
mov ft0.w, v1.w		
nrm ft1.xyz, v2
mov ft1.w,   v2.w 		
dp3 ft3.x, fc2, ft0
max ft3.w, ft3.x, fc0.y
mul ft3.w, ft3.w, fc2.w
mul ft3, ft3.w, fc3
add ft4, fc2, ft1
nrm ft4.xyz, ft4
dp3 ft4.w, ft0, ft4
sat ft4.w, ft4.w
pow ft4.w, ft4.w, fc1.w
mul ft4.w, ft4.w, fc2.w
mul ft4.xyz, fc4, ft4.w
mov ft2, fc5
tex ft5, v0, fs0 <2d,linear,wrap>
sat ft3, ft3
add ft2.xyz, ft3, ft2
mul ft2.xyz, ft5, ft2
mov ft2.w, ft5.w
mul ft4.xyz, ft4, fc1
add ft2.xyz, ft2, ft4
mov oc, ft2
```

## 获取Stage3D渲染代码

打开Adobe Scout CC获取Stage3D渲染代码

![](http://www.feng3d.me/wordpress/wp-content/uploads/2014/09/20140916103957-300x193.jpg)

```
Context3D.clear(0, 0, 0, 1, 1, 0, ALL)
Context3D.setDepthTest(false, ALWAYS)
Context3D.setScissorRectangle(0, 0, 0, 0)
Context3D.setBlendFactors(1, 0)
Context3D.setDepthTest(true, LESSEQUAL)
Context3D.setDepthTest(true, LESSEQUAL)
Context3D.setProgram(1)
Context3D.setCulling(BACK)
Context3D.setTextureAt(0, 1)
Context3D.setVertexBufferAt(2, 1, 9, FLOAT_2)
Context3D.setVertexBufferAt(1, 1, 3, FLOAT_3)
Context3D.setProgramConstantsDouble(VERTEX, 0, 13)
Context3D.setProgramConstantsDouble(FRAGMENT, 0, 6)
Context3D.setVertexBufferAt(0, 1, 0, FLOAT_3)
Context3D.drawTriangles(1, 0, 2)
Context3D.setDepthTest(true, LESSEQUAL)
Context3D.setDepthTest(false, LESSEQUAL)
Context3D.setDepthTest(true, LESSEQUAL)
Context3D.setDepthTest(false, LESSEQUAL)
Context3D.setScissorRectangle(0, 0, 0, 0)
Context3D.setVertexBufferAt(0, 0, 0, DISABLED)
Context3D.setTextureAt(0, 0)
Context3D.setVertexBufferAt(1, 0, 0, DISABLED)
Context3D.setTextureAt(1, 0)
Context3D.setVertexBufferAt(2, 0, 0, DISABLED)
Context3D.setTextureAt(2, 0)
Context3D.setVertexBufferAt(3, 0, 0, DISABLED)
Context3D.setTextureAt(3, 0)
Context3D.setVertexBufferAt(4, 0, 0, DISABLED)
Context3D.setTextureAt(4, 0)
Context3D.setVertexBufferAt(5, 0, 0, DISABLED)
Context3D.setTextureAt(5, 0)
Context3D.setVertexBufferAt(6, 0, 0, DISABLED)
Context3D.setTextureAt(6, 0)
Context3D.setVertexBufferAt(7, 0, 0, DISABLED)
Context3D.setTextureAt(7, 0)
Context3D.present()
```

## 分析stage3D渲染代码，理解传入到stage3D的数据

### 分析stage3d渲染代码结果
先忽略contex3d的setDepthTest、setScissorRectangle、setBlendFactors、setCulling方法。

只关注传递给context3d的数据相关方法。

```
Context3D.clear(0, 0, 0, 1, 1, 0, ALL)
//设置context3d属性
Context3D.setDepthTest(false, ALWAYS)
Context3D.setScissorRectangle(0, 0, 0, 0)
Context3D.setBlendFactors(1, 0)
Context3D.setDepthTest(true, LESSEQUAL)
Context3D.setDepthTest(true, LESSEQUAL)
//设置渲染程序
Context3D.setProgram(1)
Context3D.setCulling(BACK)
//设置纹理fs0
Context3D.setTextureAt(0, 1)
//va2：uv坐标
Context3D.setVertexBufferAt(2, 1, 9, FLOAT_2)
//va1：法线
Context3D.setVertexBufferAt(1, 1, 3, FLOAT_3)
//上传了52个number，13个vector。0-15（vc0）：投影矩阵，16-31（vc4）：物体场景转换矩阵，32-47（vc8）：法线场景转换矩阵，48-51（vc12）：照相机全局坐标（w为1）
Context3D.setProgramConstantsDouble(VERTEX, 0, 13)
//fc0:公共数据(0.5,0,1/255,1),
//fc1:高光色(_specularR,_specularG,_specularB,_gloss) 
//fc2:光照场景方向(-dirPos.x,-dirPos.y,-dirPos.z,1)，
//fc3:漫反射光颜色(dirLight._diffuseR,dirLight._diffuseG,dirLight._diffuseB,1)，
//fc4:镜面反射颜色(dirLight._specularR,dirLight._specularG,dirLight._specularB,1)，
//fc5:环境光颜色（_ambientR,_ambientG,_ambientB,1）
Context3D.setProgramConstantsDouble(FRAGMENT, 0, 6)
//va0：顶点坐标
Context3D.setVertexBufferAt(0, 1, 0, FLOAT_3)
Context3D.drawTriangles(1, 0, 2)
//清除context3d
Context3D.setDepthTest(true, LESSEQUAL)
Context3D.setDepthTest(false, LESSEQUAL)
Context3D.setDepthTest(true, LESSEQUAL)
Context3D.setDepthTest(false, LESSEQUAL)
Context3D.setScissorRectangle(0, 0, 0, 0)
Context3D.setVertexBufferAt(0, 0, 0, DISABLED)
Context3D.setTextureAt(0, 0)
Context3D.setVertexBufferAt(1, 0, 0, DISABLED)
Context3D.setTextureAt(1, 0)
Context3D.setVertexBufferAt(2, 0, 0, DISABLED)
Context3D.setTextureAt(2, 0)
Context3D.setVertexBufferAt(3, 0, 0, DISABLED)
Context3D.setTextureAt(3, 0)
Context3D.setVertexBufferAt(4, 0, 0, DISABLED)
Context3D.setTextureAt(4, 0)
Context3D.setVertexBufferAt(5, 0, 0, DISABLED)
Context3D.setTextureAt(5, 0)
Context3D.setVertexBufferAt(6, 0, 0, DISABLED)
Context3D.setTextureAt(6, 0)
Context3D.setVertexBufferAt(7, 0, 0, DISABLED)
Context3D.setTextureAt(7, 0)
//绘制到stage3d
Context3D.present()
```

### 分析stage3d渲染代码方法及过程

program：渲染程序就是段落2中给出的AGAL代码

fs0：stage3d渲染代码中只有一个setTexture函数，毫无疑问就是我们在程序中给网格指定的纹理了

va0、va1、va2（顶点属性数据）：该三行代码表示了给contex3d设置了三个顶点数据va0、va1、va2，并且从第二个参数都为1可以看出是使用同一个顶点缓冲VertexBuffer的。

```
Context3D.setVertexBufferAt(2, 1, 9, FLOAT_2)
Context3D.setVertexBufferAt(1, 1, 3, FLOAT_3)
Context3D.setVertexBufferAt(0, 1, 0, FLOAT_3)
```

![](http://www.feng3d.me/wordpress/wp-content/uploads/2014/09/20140916111644-300x196.jpg)

为了弄清楚这三个数据到底是什么我采用了最笨的方法，使用falsh builder查找context.setVertexBufferAt调用层次结构，在使用该方法的没处设置断点，再运行实例根据断点查看这三个数据，最后得出结果：

```
//va2：uv坐标
Context3D.setVertexBufferAt(2, 1, 9, FLOAT_2)
//va1：法线
Context3D.setVertexBufferAt(1, 1, 3, FLOAT_3)
//va0：顶点坐标 
Context3D.setVertexBufferAt(0, 1, 0, FLOAT_3)
```

vc0、vc4、vc8、vc12（顶点常量数据）：代码中上传了13个向量（52个number）、使用到的函数是setProgramConstantsFromVector并不是setProgramConstantsDouble（一开始把我给忽悠着了）！

```
Context3D.setProgramConstantsDouble(VERTEX, 0, 13)
```

这行stage3d代码对应在away3d.materials.passes::CompiledPass/render

```
context.setProgramConstantsFromVector(Context3DProgramType.VERTEX, 0, _vertexConstantData, _numUsedVertexConstants);
```

_vertexConstantData便是长度为52的Vector.<Number>数据

接下来便是跟踪_vertexConstantData 数据（查找调用层次结构等等），查找出每一个对其修改的地方，最后得出结果为

0-15（vc0）：投影矩阵，16-31（vc4）：物体场景转换矩阵，32-47（vc8）：法线场景转换矩阵，48-51（vc12）：照相机全局坐标（w为1）

fc0、fc1、fc2、fc3、fc4、fc5（片段常量数据）：代码中上传了6个向量（24个namber）

```
Context3D.setProgramConstantsDouble(FRAGMENT, 0, 6)
```

```
context.setProgramConstantsFromVector(Context3DProgramType.FRAGMENT, 0, _fragmentConstantData, _numUsedFragmentConstants);
```

与分析顶点常量数据方式类似，最后得到结果为

fc0:公共数据(0.5,0,1/255,1)           away3d.materials.passes::CompiledPass /initCommonsData

```
protected function initCommonsData():void
{
    _fragmentConstantData[_commonsDataIndex] = .5;
    _fragmentConstantData[_commonsDataIndex + 1] = 0;
    _fragmentConstantData[_commonsDataIndex + 2] = 1/255;
    _fragmentConstantData[_commonsDataIndex + 3] = 1;
}
```

fc1：高光色(_specularR,_specularG,_specularB,_gloss)        away3d.materials.methods::BasicSpecularMethod/activate

```
var index:int = vo.fragmentConstantsIndex;
var data:Vector.<Number> = vo.fragmentData;
data[index] = _specularR;
data[index + 1] = _specularG;
data[index + 2] = _specularB;
data[index + 3] = _gloss;
```

fc2:光照场景方向(-dirPos.x,-dirPos.y,-dirPos.z,1)

fc3：漫反射光颜色(dirLight._diffuseR,dirLight._diffuseG,dirLight._diffuseB,1)

fc4：镜面反射颜色(dirLight._specularR,dirLight._specularG,dirLight._specularB,1)

away3d.materials.passes::SuperShaderPass/updateLightConstants

```
_fragmentConstantData[k++] = -dirPos.x;
_fragmentConstantData[k++] = -dirPos.y;
_fragmentConstantData[k++] = -dirPos.z;
_fragmentConstantData[k++] = 1;

_fragmentConstantData[k++] = dirLight._diffuseR;
_fragmentConstantData[k++] = dirLight._diffuseG;
_fragmentConstantData[k++] = dirLight._diffuseB;
_fragmentConstantData[k++] = 1;

_fragmentConstantData[k++] = dirLight._specularR;
_fragmentConstantData[k++] = dirLight._specularG;
_fragmentConstantData[k++] = dirLight._specularB;
_fragmentConstantData[k++] = 1;
```

fc5:环境光颜色（_ambientR,_ambientG,_ambientB,1）

```
override arcane function setRenderState(vo:MethodVO, renderable:IRenderable, stage3DProxy:Stage3DProxy, camera:Camera3D):void
{
    updateAmbient();
    
    if (!_useTexture) {
        var index:int = vo.fragmentConstantsIndex;
        var data:Vector.<Number> = vo.fragmentData;
        data[index] = _ambientR;
        data[index + 1] = _ambientG;
        data[index + 2] = _ambientB;
    }
}
```

away3d.materials.methods::BasicAmbientMethod/initConstants

```
override arcane function initConstants(vo:MethodVO):void
{
    vo.fragmentData[vo.fragmentConstantsIndex + 3] = 1;
}
```

## 分析AGAL代码

### 分析AGAL代码结果

```
//输入
//va0:顶点坐标
//va1:法线
//va2:uv坐标
//vc4:物体场景转换矩阵
//vc0:投影矩阵
//vc8:法线场景转换矩阵
//vc12:照相机全局坐标（w为1）
 
//顶点坐标(vt0) = 顶点坐标(va0)
mov vt0, va0
//法线(vt1) = 法线(va1)
mov vt1, va1
//uv坐标(v0) = uv坐标(va2)
mov v0,va2
//顶点场景坐标(vt2) = 顶点坐标(vt0) m44 物体场景转换矩阵(vc4) 
m44 vt2, vt0, vc4
//投影坐标(op) = 物体场景坐标(vt2) m44 投影矩阵(vc0)
m44 op, vt2, vc0		
//场景法线(v1) = 法线(vt1) m33 法线场景转换矩阵(vc8)
m33 v1.xyz, vt1, vc8
//法线w值保持不变
mov v1.w, vt1.w	
// 照相机相对顶点坐标(v2) = 照相机全局坐标(vc12) - 顶点场景坐标(vt2)
sub v2, vc12, vt2
 
 
--------------------
//输入
//v0:uv坐标
//v1:场景法线
//v2:照相机相对顶点坐标(视线方向)
//fc0:公共数据(0.5,0,1/255,1)
//fc1:材质镜面反射颜色(_specularR,_specularG,_specularB,_gloss)   
//fc2:光照场景方向(-dirPos.x,-dirPos.y,-dirPos.z,1)
//fc3:灯光漫反射光颜色(dirLight._diffuseR,dirLight._diffuseG,dirLight._diffuseB,1)
//fc4:灯光镜面反射颜色(dirLight._specularR,dirLight._specularG,dirLight._specularB,1)
//fc5:环境光颜色(_ambientR,_ambientG,_ambientB,1)
//fs0:贴图纹理
 
//标准法线(ft0) = nrm 场景法线(v1)
nrm ft0.xyz, v1
//保持w值不变
mov ft0.w, v1.w
//标准视线方向(ft1) = nrm 视线方向(v2)
nrm ft1.xyz, v2
//保持w值不变
mov ft1.w,   v2.w 		
 
//计算漫反射光
//法线与光照夹角余弦值(ft3.x) = 光照方向(fc2) dp3 法线(ft0)
dp3 ft3.x, fc2, ft0
//取出夹角小于90度的部分 余弦值>0  ft3.w = max(余弦值(ft3.x),fc0.y) //此处fc0.y= 0
max ft3.w, ft3.x, fc0.y
//漫反射权重(ft3.w) = 余弦值(ft3.w) mul (入射光强度？)fc2.w
mul ft3.w, ft3.w, fc2.w
//漫反射颜色(ft3) = 漫反射权重(ft3.w) mul 漫反射光颜色(fc3)
mul ft3, ft3.w, fc3
 
//计算镜面反射光
//入射光与视线方向的和(ft4) = 光照场景方向(fc2) add 标准视线方向(ft1)
add ft4, fc2, ft1
//标准化入射光与视线的和
nrm ft4.xyz, ft4
//镜面反射光强度(ft4.w) = 法线(ft0) dp3 入射光与视线方向的和(ft4)
dp3 ft4.w, ft0, ft4
//镜面反射光强度(ft4.w) 锁定在0-1之间
sat ft4.w, ft4.w
 
//镜面反射光强度(ft4.w) = 镜面反射光强度(ft4.w) pow 光泽度(fc1.w)
pow ft4.w, ft4.w, fc1.w
//镜面反射光强度(ft4.w) = 镜面反射光强度(ft4.w) pow (入射光强度？)fc2.w
mul ft4.w, ft4.w, fc2.w
//镜面反射光颜色(ft4.xyz) = 灯光镜面反射颜色(fc4) mul 镜面反射光强度(ft4.w)
mul ft4.xyz, fc4, ft4.w
 
 
//环境光颜色(ft2) = 环境光颜色(fc5)
mov ft2, fc5
//材质颜色(ft5)
tex ft5, v0, fs0 <2d,linear,wrap>
//漫反射颜色(ft3) 锁定在0-1之间
sat ft3, ft3
//环境加漫反射光颜色(ft2) = 漫反射颜色(ft3) add 环境光颜色(ft2)
add ft2.xyz, ft3, ft2
//混合环境加漫反射光颜色(ft2.xyz) = 材质颜色(ft5) mul 环境加漫反射光颜色(ft2)
mul ft2.xyz, ft5, ft2
//保持w值不变
mov ft2.w, ft5.w
 
//混合镜面反射光(ft4.xyz) = 镜面反射光颜色(ft4.xyz) mul 材质镜面反射颜色(fc1)
mul ft4.xyz, ft4, fc1
//混合环境加漫反射镜面反射光 = 混合环境加漫反射光颜色(ft2.xyz) + 混合镜面反射光(ft4.xyz)
add ft2.xyz, ft2, ft4
mov oc, ft2
```

### 分析过程

根据第4节中提供的输入数据一行一行AGAL代码进行分析理解，分析过程参考Stage3D-3D光照。

## away3d简单光照分析完毕

光照原理与AGAL代码分析完毕，接下来该想办法实现光照功能并融合到feng3d中。

在此感谢VanCopper ，他的Stage3D-3D光照文章给我分析光照提供了很大的帮助！