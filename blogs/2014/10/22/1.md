# 实现法线贴图

![](http://images.feng3d.me/wordpress/wp-content/uploads/2014/10/20141022112653.jpg)

[查看实例](http://images.feng3d.me/feng3dDemo/PointLightTest.html)

该实例和点光源实例使用的是同一套代码，只是加了一句

```
planeMaterial.normalMap = Cast.bitmapTexture(FloorNormals);
```

实现该功能的根本就是使用法线贴图代替原来的顶点法线数据来计算法线与光线、法线与视线的角度，从而影响光照的漫反射与镜面反射效果。

以下为关键代码：

```
    /**
    * 编译切线顶点程序
    */
private function compileTangentVertexCode():void
{
    //顶点法线寄存器
    var normalInput:ShaderRegister = reg(Context3DBufferTypeID.NORMAL_VA_3);
    //顶点法线寄存器
    var tangentInput:ShaderRegister = reg(Context3DBufferTypeID.TANGENT_VA_3);
    //法线全局转换矩阵寄存器
    var matrix:ShaderRegister = reg(Context3DBufferTypeID.NORMALGLOBALTRANSFORM_VC_MATRIX);

    //动画后顶点法线临时寄存器
    var animatedNormal:ShaderRegister = reg(Context3DBufferTypeID.ANIMATEDNORMAL_VT_4);
    //动画后顶点切线临时寄存器
    var animatedTangent:ShaderRegister = reg(Context3DBufferTypeID.ANIMATEDTANGENT_VT_4);

    //法线变量寄存器
    var normalVarying:ShaderRegister = reg(Context3DBufferTypeID.NORMAL_V);
    //切线变量寄存器
    var tangentVarying:ShaderRegister = reg(Context3DBufferTypeID.TANGENT_V);
    //切线变量寄存器
    var bitangentVarying:ShaderRegister = reg(Context3DBufferTypeID.BITANGENT_V);

    //动画后顶点双切线临时寄存器
    var bitanTemp:ShaderRegister = reg(Context3DBufferTypeID.ANIMATEDBITANGENT_VT_4);

    //赋值法线数据
    _vertexCode += "mov " + animatedNormal + ", " + normalInput  + "\n";
    //赋值切线数据
    _vertexCode += "mov " + animatedTangent + ", " + tangentInput  + "\n";
    
    
    //计算顶点世界法线 vc8：模型转世界矩阵
    _vertexCode += "m33 " + animatedNormal + ".xyz, " + animatedNormal + ", " + matrix + "\n" + //
        //标准化顶点世界法线
        "nrm " + animatedNormal + ".xyz, " + animatedNormal + "\n";

    //计算顶点世界切线
    _vertexCode += "m33 " + animatedTangent + ".xyz, " + animatedTangent + ", " + matrix + "\n" + //
        //标准化顶点世界切线
        "nrm " + animatedTangent + ".xyz, " + animatedTangent + "\n";

    //计算切线x
    _vertexCode += "mov " + tangentVarying + ".x, " + animatedTangent + ".x  \n" + //
        //计算切线z
        "mov " + tangentVarying + ".z, " + animatedNormal + ".x  \n" + //
        //计算切线w
        "mov " + tangentVarying + ".w, " + normalInput + ".w  \n" + //
        //计算双切线x
        "mov " + bitangentVarying + ".x, " + animatedTangent + ".y  \n" + //
        //计算双切线z
        "mov " + bitangentVarying + ".z, " + animatedNormal + ".y  \n" + //
        //计算双切线w
        "mov " + bitangentVarying + ".w, " + normalInput + ".w  \n" + //
        //计算法线x
        "mov " + normalVarying + ".x, " + animatedTangent + ".z  \n" + //
        //计算法线z
        "mov " + normalVarying + ".z, " + animatedNormal + ".z  \n" + //
        //计算法线w
        "mov " + normalVarying + ".w, " + normalInput + ".w  \n" + //
        //计算双切线
        "crs " + bitanTemp + ".xyz, " + animatedNormal + ", " + animatedTangent + "\n" + //
        //计算切线y
        "mov " + tangentVarying + ".y, " + bitanTemp + ".x    \n" + //
        //计算双切线y
        "mov " + bitangentVarying + ".y, " + bitanTemp + ".y  \n" + //
        //计算法线y
        "mov " + normalVarying + ".y, " + bitanTemp + ".z    \n"; //

    registerCache.removeTempUsage(bitanTemp);
}
```

```
		/**
		 * 编译切线法线贴图片段程序
		 */
		private function compileTangentNormalMapFragmentCode():void
		{
			//切线变量寄存器
			var tangentVarying:ShaderRegister = reg(Context3DBufferTypeID.TANGENT_V);
			//切线变量寄存器
			var bitangentVarying:ShaderRegister = reg(Context3DBufferTypeID.BITANGENT_V);
			//法线变量寄存器
			var normalVarying:ShaderRegister = reg(Context3DBufferTypeID.NORMAL_V);

			//法线变量寄存器
			var normalFragment:ShaderRegister = reg(Context3DBufferTypeID.NORMAL_FT_4);

			//切线片段临时寄存器
			var t:ShaderRegister = registerCache.getFreeFragmentTemp();
			//双切线片段临时寄存器
			var b:ShaderRegister = registerCache.getFreeFragmentTemp();
			//法线片段临时寄存器
			var n:ShaderRegister = registerCache.getFreeFragmentTemp();

			//标准化切线
			_fragmentCode += "nrm " + t + ".xyz, " + tangentVarying + "\n" + //
				//保存w不变
				"mov " + t + ".w, " + tangentVarying + ".w	\n" + //
				//标准化双切线
				"nrm " + b + ".xyz, " + bitangentVarying + "\n" + //
				//标准化法线
				"nrm " + n + ".xyz, " + normalVarying + "\n" +//
				"";

			//法线纹理数据片段临时寄存器
			var normalTexData:ShaderRegister = reg(Context3DBufferTypeID.NORMALTEXDATA_FT_4);
			//获取法线纹理数据
			_fragmentCode += _methodSetup._normalMethod.getFragmentCode(_methodSetup._normalMethodVO, registerCache) + //
				//标准化法线纹理数据
				"m33 " + normalFragment + ".xyz, " + normalTexData + ", " + t + "	\n" + //
				//保存w不变
				"mov " + normalFragment + ".w,   " + normalVarying + ".w			\n";

			registerCache.removeTempUsage(t);
			registerCache.removeTempUsage(b);
			registerCache.removeTempUsage(n);
		}
```

```
		override arcane function getFragmentCode(vo:MethodVO, regCache:ShaderRegisterCache):String
		{
			//常用数据寄存器
			var commons:ShaderRegister = regCache.requestRegister(Context3DBufferTypeID.COMMONSDATA_FC_VECTOR);
			//法线纹理数据片段临时寄存器
			var normalTexDataReg:ShaderRegister = regCache.requestRegister(Context3DBufferTypeID.NORMALTEXDATA_FT_4);

			//漫反射纹理寄存器
			_normalTextureRegister = regCache.requestRegister(Context3DBufferTypeID.NORMALTEXTURE_FS);
			
			//获取纹理数据
			return getTex2DSampleCode(vo, normalTexDataReg, _normalTextureRegister, _texture) + //
				//使法线数据 【0,1】->【-0.5,0.5】
				"sub " + normalTexDataReg + ".xyz, " + normalTexDataReg + ".xyz, " + commons + ".xxx	\n" + //
				//标准化法线纹理数据
				"nrm " + normalTexDataReg + ".xyz, " + normalTexDataReg + ".xyz	\n"; //
		}
```

参考：

[实现点光源](blogs/2014/10/09/1.md)

[away3d源码](https://github.com/away3d/away3d-core-fp11/tree/dev)

[法线贴图原理](http://www.cnblogs.com/lancidie/archive/2010/11/02/1866914.html)