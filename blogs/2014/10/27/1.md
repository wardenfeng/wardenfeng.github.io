# Fagal首个实例BaseShaderTest

![](http://images.feng3d.me/wordpress/wp-content/uploads/2014/10/20141027163235.jpg)

[查看实例](http://images.feng3d.me/feng3dDemo/BaseShaderTest.html)

使用键盘左上角“~”键可以查看渲染代码。

该渲染实例是feng3d渲染过程的一个极简版本，实现了EazyAGAL中Example01实例。

顶点渲染程序

```
package fagal
{
	import me.feng3d.core.register.Register;
	import me.feng3d.fagal.methods.FagalMethod;

	/**
	 * 基础顶点渲染
	 * @author warden_feng 2014-10-24
	 */
	[FagalMethod(methodName = "V_baseShader", methodType = "vertex")]
	public class V_baseShader extends FagalMethod
	{
		[Register(regName = "position_va_3", regType = "in", description = "顶点坐标数据")]
		public var position:Register;

		[Register(regName = "color_va_3", regType = "in", description = "顶点颜色数据")]
		public var color:Register;

		[Register(regName = "color_v", regType = "out", description = "颜色变量寄存器")]
		public var color_v:Register;

		[Register(regName = "projection_vc_matrix", regType = "uniform", description = "顶点程序投影矩阵静态数据")]
		public var projection:Register;

		[Register(regName = "op", regType = "out", description = "位置输出寄存器")]
		public var out:Register;

		override public function runFunc():void
		{
			comment("应用投影矩阵" + projection + "使世界坐标" + position + "转换为投影坐标 并输出到顶点寄存器" + out + "");
			m44(out, position, projection);

			comment("传递顶点颜色数据" + color + "到变量寄存器" + color_v);
			mov(color_v, color);
		}
	}
}
```

片段渲染程序

```
package fagal
{
	import me.feng3d.core.register.Register;
	import me.feng3d.fagal.methods.FagalMethod;

	/**
	 * 基础片段渲染
	 * @author warden_feng 2014-10-24
	 */
	[FagalMethod(methodName = "F_baseShader", methodType = "fragment")]
	public class F_baseShader extends FagalMethod
	{
		[Register(regName = "color_v", regType = "in", description = "颜色变量寄存器")]
		public var color_v:Register;

		[Register(regName = "oc", regType = "out", description = "颜色输出寄存器")]
		public var out:Register;

		override public function runFunc():void
		{
			comment("传递顶点颜色数据" + color_v + "到片段寄存器" + out);
			mov(out, color_v);
		}
	}
}
```

源码地址： [http://pan.baidu.com/s/1i3IcDYt](http://pan.baidu.com/s/1i3IcDYt)

参考：

1. [EasyAGAL](https://github.com/Barliesque/EasyAGAL)

2. Cg语言

3. [Fagal](fagal.md)