# 实现点光源

![](http://images.feng3d.me/wordpress/wp-content/uploads/2014/10/20141009174254.jpg)

[查看实例](http://images.feng3d.me/feng3dDemo/PointLightTest.html)

点光源与方向光的差异有以下两点：

1. 点光源光的方向需要计算，方向光的方向是直接给出的。

2. 点光源总是伴随着光强的衰减，方向光不一定。

处理好了这两点，其他的地方与方向光一样。

 
关键代码：

```
/**
		 * 编译点光源代码
		 */
		private function compilePointLightCode(regCache:ShaderRegisterCache):void
		{
			//获取点光源漫反射颜色寄存器
			var dirLightDiffuseColorHeadReg:ShaderRegister = regCache.requestRegister(Context3DBufferTypeID.POINTLIGHTDIFFUSE_FC_VECTOR, _numPointLights);
			//获取点光源方向寄存器
			var dirLightScenePosHeadReg:ShaderRegister = regCache.requestRegister(Context3DBufferTypeID.POINTLIGHTSCENEPOS_FC_VECTOR, _numPointLights);
			//获取点光源镜面反射颜色寄存器
			var dirLightSpecularColorHeadReg:ShaderRegister = regCache.requestRegister(Context3DBufferTypeID.POINTLIGHTSPECULAR_FC_VECTOR, _numPointLights);
			//全局坐标变量
			var globalPosVaryReg:ShaderRegister = reg(Context3DBufferTypeID.GLOBALPOS_V);
			
			//光线方向
			var lightDirReg:ShaderRegister;
			//点光源漫反射颜色寄存器
			var diffuseColorReg:ShaderRegister;
			//点光源镜面反射颜色寄存器
			var specularColorReg:ShaderRegister;
			//点光源全局坐标寄存器
			var lightPosReg:ShaderRegister;

			var regIndex:int;

			var addSpec:Boolean = _usingSpecularMethod && usesLightsForSpecular();
			var addDiff:Boolean = usesLightsForDiffuse();

			if (!(addSpec || addDiff))
				return;

			for (var i:uint = 0; i < _numPointLights; ++i)
			{
				lightPosReg = dirLightScenePosHeadReg.getReg(i);
				diffuseColorReg = dirLightDiffuseColorHeadReg.getReg(i);
				specularColorReg = dirLightSpecularColorHeadReg.getReg(i);
				lightDirReg = regCache.getFreeFragmentTemps(1)[0];

				// 计算光衰减 （根据物体表面离光源的距离来计算光强）
				//物体表面到光源距离
				_fragmentCode += "sub " + lightDirReg + ", " + lightPosReg + ", " + globalPosVaryReg + "\n" +
					// attenuate
					"dp3 " + lightDirReg + ".w, " + lightDirReg + ", " + lightDirReg + "\n" +
					// w = d - radis
					"sub " + lightDirReg + ".w, " + lightDirReg + ".w, " + diffuseColorReg + ".w\n" +
					// w = (d - radius)/(max-min) 
					"mul " + lightDirReg + ".w, " + lightDirReg + ".w, " + specularColorReg + ".w\n" +
					// w = clamp(w, 0, 1)
					"sat " + lightDirReg + ".w, " + lightDirReg + ".w\n" +
					// w = 1-w (w为光强)
					"sub " + lightDirReg + ".w, " + lightPosReg + ".w, " + lightDirReg + ".w\n" +
					// normalize
					"nrm " + lightDirReg + ".xyz, " + lightDirReg + "\n";

				//计算漫反射
				if (addDiff)
					_fragmentCode += _methodSetup._diffuseMethod.getFragmentCodePerLight(_methodSetup._diffuseMethodVO, lightDirReg, diffuseColorReg, registerCache);

				//计算镜面反射
				if (addSpec)
					_fragmentCode += _methodSetup._specularMethod.getFragmentCodePerLight(_methodSetup._specularMethodVO, lightDirReg, specularColorReg, registerCache);

				regCache.removeTempUsage(lightDirReg);
			}
		}
```

参考：

[实现方向光照](blogs/2014/10/09/2.md)

[away3d源码](https://github.com/away3d/away3d-core-fp11/tree/dev)