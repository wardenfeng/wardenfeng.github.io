# 高度地形实现方式
![](http://images.feng3d.me/wordpress/wp-content/uploads/2014/08/20140819103718-300x223.jpg)

[查看实例](http://images.feng3d.me/feng3dDemo/TerrainTest.html)

该实例是模仿away3d实现的，资源采用的是嵌入方式，swf文件有4m多，加载过程比较缓慢。 资源： 地形高度图：terrain_heights.jpg 地形贴图：terrain_diffuse.jpg 地形法线图：terrain_normals.jpg 地形土壤类型——草地：grass.jpg 地形土壤类型——岩石：rock.jpg 地形土壤类型——海滩：beach.jpg 地形混合贴图：terrain_splats.png   根据高度图创建网格，代码见(feng3d)Elevation.buildGeometry()

```
/**
* 创建顶点坐标
*/		
private function buildGeometry():void
{
    var vertices:Vector.<Number>;
    var indices:Vector.<uint>;
    var x:Number, z:Number;
    var numInds:uint;
    var base:uint;
    //一排顶点数据
    var tw:uint = _segmentsW + 1;
    //总顶点数量
    var numVerts:uint = (_segmentsH + 1) * tw;
    //一个格子所占高度图X轴像素数
    var uDiv:Number = (_heightMap.width - 1) / _segmentsW;
    //一个格子所占高度图Y轴像素数
    var vDiv:Number = (_heightMap.height - 1) / _segmentsH;
    var u:Number, v:Number;
    var y:Number;

    if (numVerts == _subGeometry.numVertices)
    {
        vertices = _subGeometry.vertexData;
        indices = _subGeometry.indexData;
    }
    else
    {
        vertices = new Vector.<Number>(numVerts * 3, true);
        indices = new Vector.<uint>(_segmentsH * _segmentsW * 6, true);
    }

    numVerts = 0;
    var col:uint;

    for (var zi:uint = 0; zi <= _segmentsH; ++zi)
    {
        for (var xi:uint = 0; xi <= _segmentsW; ++xi)
        {
            //顶点坐标
            x = (xi / _segmentsW - .5) * _width;
            z = (zi / _segmentsH - .5) * _depth;
            //格子对应高度图uv坐标
            u = xi * uDiv;
            v = (_segmentsH - zi) * vDiv;

            //获取颜色值
            col = _heightMap.getPixel(u, v) & 0xff;
            //计算高度值
            y = (col > _maxElevation) ? (_maxElevation / 0xff) * _height : ((col < _minElevation) ? (_minElevation / 0xff) * _height : (col / 0xff) * _height);

            //保存顶点坐标
            vertices[numVerts++] = x;
            vertices[numVerts++] = y;
            vertices[numVerts++] = z;

            if (xi != _segmentsW && zi != _segmentsH)
            {
                //增加 一个顶点同时 生成一个格子或两个三角形
                base = xi + zi * tw;
                indices[numInds++] = base;
                indices[numInds++] = base + tw;
                indices[numInds++] = base + tw + 1;
                indices[numInds++] = base;
                indices[numInds++] = base + tw + 1;
                indices[numInds++] = base + 1;
            }
        }
    }

    _subGeometry.autoDeriveVertexNormals = true;
    _subGeometry.autoDeriveVertexTangents = true;
    _subGeometry.updateVertexData(vertices);
    _subGeometry.updateIndexData(indices);
}
```

创建uv，代码见(feng3d)Elevation.buildUVs()

```
   /**
    * 创建uv坐标
    */
private function buildUVs():void
{
    var uvs:Vector.<Number> = new Vector.<Number>();
    var numUvs:uint = (_segmentsH + 1) * (_segmentsW + 1) * 2;

    if (_subGeometry.UVData && numUvs == _subGeometry.UVData.length)
        uvs = _subGeometry.UVData;
    else
        uvs = new Vector.<Number>(numUvs, true);

    numUvs = 0;
    //计算每个顶点的uv坐标
    for (var yi:uint = 0; yi <= _segmentsH; ++yi)
    {
        for (var xi:uint = 0; xi <= _segmentsW; ++xi)
        {
            uvs[numUvs++] = xi / _segmentsW;
            uvs[numUvs++] = 1 - yi / _segmentsH;
        }
    }

    _subGeometry.updateUVData(uvs);
}
```

渲染地形，代码见(feng3d)TerrainDiffuseMethod.getFragmentPostLightingCode()

```
arcane override function getFragmentPostLightingCode(vo:MethodVO, regCache:ShaderRegisterCache, targetReg:ShaderRegister):String
{
    //uv 变量数据
    var uvVarying:ShaderRegister = regCache.requestRegister(RegisterDataType.UV_V);
    // 默认纹理
    var defaultFsReg:ShaderRegister = regCache.requestRegister(textureReg);
    // 混合纹理
    var blendingFsReg:ShaderRegister = regCache.requestRegister(nBlendingTextureReg);
    //纹理缩放比例
    var scaleRegister:ShaderRegister = regCache.requestRegister(tileDataReg);

    var i:int;
    var splatFsarr:Array = [];
    for (i = 0; i < _numSplattingLayers; ++i)
        splatFsarr[i] = regCache.requestRegister(nSplatTextureRegArr[i]);

    //计算需要，获取两个临时寄存器
    var tempVts:Vector.<ShaderRegister> = regCache.getFreeFragmentTemps(2);

    //缩放后的uv坐标、uv计算后的颜色值
    var uvTemp:ShaderRegister = tempVts[0];
    //混合比例（4个分量表示4个纹理所占比重）
    var blendTemp:ShaderRegister = tempVts[1];

    var code:String = "";
    code += "mul " + uvTemp + ", " + uvVarying + ", " + scaleRegister.c(0) + "\n"; // 缩放uv （表示将重复x*x遍） x=scaleRegister.c(0)
    code += "tex " + targetReg + ", " + uvTemp + ", " + defaultFsReg + " <2d,linear,miplinear,wrap>\n"; // 使用默认贴图 得到默认颜色值
    code += "tex " + blendTemp + ", " + uvVarying + ", " + blendingFsReg + " <2d,linear,miplinear,clamp>\n"; // 计算混合纹理

    for (i = 0; i < _numSplattingLayers; ++i)
    {
        code += "mul " + uvTemp + ", " + uvVarying + "," + scaleRegister.c(i + 1) + "\n"; //  缩放uv （表示将重复x*x遍） x=scaleRegister.c(i + 1)
        code += "tex " + uvTemp + ", " + uvTemp + ", " + splatFsarr[i] + " <2d,linear,miplinear,wrap>\n"; // 使用地面纹理 得到该纹理颜色值
        
        code += "sub " + uvTemp + ", " + uvTemp + ", " + targetReg + "\n"; // uvtemp = uvtemp - targetreg; --------------1
        code += "mul " + uvTemp + ", " + uvTemp + ", " + blendTemp.c(i) + "\n"; // uvtemp = uvtemp * blendtemp; ----------2  (0 <= blendtemp <= 1)
        code += "add " + targetReg + ", " + targetReg + ", " + uvTemp + "\n"; // 添加到默认颜色值上  targetreg = targetreg + uvtemp; ------------3
        //由 1代入2得		uvtemp = (uvtemp - targetreg) * blendtemp; ----------------4
        //由 4代入3得		targetreg = targetreg + (uvtemp - targetreg) * blendtemp; -------------------5
        //整理5得			targetreg = targetreg * (1 - blendtemp) + uvtemp * blendtemp; (0 <= blendtemp <= 1) -----------------6 
        //公式6很容易看出是平分公式，由此得 引用1、2、3的渲染代码是为了节约变量与计算次数的平分运算。(至少节约1个变量与一次运算)
    }

    return code;
}
```

执行后的渲染代码

```
Compiling AGAL Code:
--------------------
m44 op, va0, vc0
mov v1,va1
 
--------------------
mov ft1, fc0
mul ft2, v1, fc1.x
tex ft1, ft2, fs0 <2d,linear,miplinear,wrap>
tex ft3, v1, fs1 <2d,linear,miplinear,clamp>
mul ft2, v1,fc1.y
tex ft2, ft2, fs2 <2d,linear,miplinear,wrap>
sub ft2, ft2, ft1
mul ft2, ft2, ft3.x
add ft1, ft1, ft2
mul ft2, v1,fc1.z
tex ft2, ft2, fs3 <2d,linear,miplinear,wrap>
sub ft2, ft2, ft1
mul ft2, ft2, ft3.y
add ft1, ft1, ft2
mul ft2, v1,fc1.w
tex ft2, ft2, fs4 <2d,linear,miplinear,wrap>
sub ft2, ft2, ft1
mul ft2, ft2, ft3.z
add ft1, ft1, ft2
mov oc, ft1
```

由于代码中注释已经够详细，所以就不多说了。