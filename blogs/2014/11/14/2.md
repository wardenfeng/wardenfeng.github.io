# feng3d的Fagal比away3d中的AGAL对比

|	|源码理解难度|	输出debug代码理解难度|	数据寄存器(va、vc、fs、fc)分配方式|	临时寄存器释放问题|	上传数据到context3D过程|
|---|---|---|---|---|----|
|away3d中的AGAL|	非常难看懂|	几乎看不懂|	大多数写死在代码里面|	需要手动释放|	大部分需要手动指定上传编号|
|feng3d的Fagal|	直观易懂|	比较容易懂|	全自动分配，不会出现重用等问题|	自动释放|	自动指定|

接下来看看比较过程吧

那个简单一点的例子来做比较，比如天空盒渲染代码：

away3d顶点渲染代码

```javascript
/**
 * @inheritDoc
 */
arcane override function getVertexCode():String
{
        return "mul vt0, va0, vc5                \n" +
                "add vt0, vt0, vc4                \n" +
                "m44 op, vt0, vc0                \n" +
                "mov v0, va0\n";
}
```
away3d片段渲染代码

```javascript
/**
 * @inheritDoc
 */
arcane override function getFragmentCode(animationCode:String):String
{
        var format:String;
        switch (_cubeTexture.format) {
                case Context3DTextureFormat.COMPRESSED:
                        format = "dxt1,";
                        break;
                case "compressedAlpha":
                        format = "dxt5,";
                        break;
                default:
                        format = "";
        }
        var mip:String = ",mipnone";
        if (_cubeTexture.hasMipMaps)
                mip = ",miplinear";
        return "tex ft0, v0, fs0 <cube," + format + "linear,clamp" + mip + ">        \n" +
                "mov oc, ft0                                                        \n";
}
```
Fagal顶点渲染函数类

```javascript
package me.feng3d.fagal.vertex
{
        import me.feng3d.core.register.Register;
        import me.feng3d.core.register.RegisterMatrix;
        import me.feng3d.fagal.methods.FagalMethod;
 
 
        /**
         * 天空盒顶点渲染程序
         * @author warden_feng 2014-11-4
         */
        [FagalMethod(methodType = "vertex")]
        public class V_SkyBox extends FagalMethod
        {
                [Register(regName = "position_va_3", regType = "in", description = "顶点坐标数据")]
                public var position:Register;
 
                [Register(regName = "uv_v", regType = "out", description = "uv变量数据")]
                public var uv_v:Register;
 
                [Register(regName = "projection_vc_matrix", regType = "uniform", description = "顶点程序投影矩阵静态数据")]
                public var projection:RegisterMatrix;
 
                [Register(regName = "camerapos_vc_vector", regType = "uniform", description = "照相机位置静态数据")]
                public var camerapos:Register;
 
                [Register(regName = "scaleSkybox_vc_vector", regType = "uniform", description = "天空盒缩放静态数据")]
                public var scaleSkybox:Register;
 
                [Register(regName = "op", regType = "out", description = "位置输出寄存器")]
                public var out:Register;
 
                override public function runFunc():void
                {
                        var vt0:Register = getFreeTemp("缩放后的顶点坐标");
                        comment("缩放到天空盒应有的大小");
                        mul(vt0, position, scaleSkybox);
                        comment("把天空盒中心放到照相机位置");
                        add(vt0, vt0, camerapos);
                        comment("投影天空盒坐标");
                        m44(out, vt0, projection)
                        comment("占坑用的，猜的");
                        mov(uv_v, position);
                }
        }
}
```

Fagal片段渲染函数类

```javascript
package me.feng3d.fagal.fragment
{
        import me.feng3d.core.register.Register;
        import me.feng3d.fagal.methods.FagalMethod;
 
        /**
         * 天空盒片段渲染程序
         * @author warden_feng 2014-11-4
         */
        [FagalMethod(methodType = "fragment")]
        public class F_SkyBox extends FagalMethod
        {
                [Register(regName = "texture_fs", regType = "in", description = "片段程序的纹理")]
                public var texture:Register;
 
                [Register(regName = "uv_v", regType = "in", description = "uv变量数据")]
                public var uv_v:Register;
 
                [Register(regName = "oc", regType = "out", description = "颜色输出寄存器")]
                public var out:Register;
 
                override public function runFunc():void
                {
                        //获取纹理数据
                        tex(out, uv_v, texture);
                }
        }
}
```

    比较away3d与Fagal的渲染代码可以很容易看出不同来。
    away3d渲染代码非常难看懂
    Fagal渲染代码直观易懂
    away3d的数据寄存器分配都是写死的，比如va0, vc5等等
    Fagal的数据寄存器分配过程根本就看不到，因为Fagal根据”Register”原标签内容帮你分配好了，根本不用你来操心。
    away3d的临时寄存器分配也是写死的，比如vt0
    Fagal的临时寄存器分配由getFreeTemp函数分配，不用担心编号的问题，如果该临时寄存器不用了的第一时间（当然是某个Fagal函数运行结束后）自动释放掉。

比较AGALdebug代码

away3d输出AGAL代码：

```

4
5
6
7
8
9
10
11
Compiling AGAL Code:
--------------------
mov vt0, va0
mul vt0, va0, vc5                
add vt0, vt0, vc4                
m44 op, vt0, vc0                
mov v0, va0
 
--------------------
tex ft0, v0, fs0 <cube,linear,clamp,miplinear>        
mov oc, ft0
```

Fagal输出AGAL代码：

```
Compiling AGAL Code:
--------------------
//【V_SkyBox】 start
//vc0:照相机位置静态数据
//op:位置输出寄存器
//va0:顶点坐标数据
//v0:uv变量数据
//vc1-vc4:顶点程序投影矩阵静态数据
//vc5:天空盒缩放静态数据
//vt0:缩放后的顶点坐标
//缩放到天空盒应有的大小
mul vt0, va0, vc5
//把天空盒中心放到照相机位置
add vt0, vt0, vc0
//投影天空盒坐标
m44 op, vt0, vc1
//占坑用的，猜的
mov v0, va0
//【V_SkyBox】 end
 
--------------------
//【F_SkyBox】 start
//v0:uv变量数据
//oc:颜色输出寄存器
//fs0:片段程序的纹理
tex oc, v0, fs0 <cube,linear,miplinear,clamp>
//【F_SkyBox】 end
```

away3d的AGAL渲染代码清晰好看

Fagal的AGAL渲染代码中几乎每个寄存器都标注了含义，非常容易看懂，如果嫌这些啰嗦了可以调整参数来去掉这些东西。

比较上传数据到context3D过程

away3d的数据上传代码

```javascript
/**
 * @inheritDoc
 */
override arcane function render(renderable:IRenderable, stage3DProxy:Stage3DProxy, camera:Camera3D, viewProjection:Matrix3D):void
{
        var context:Context3D = stage3DProxy._context3D;
        var pos:Vector3D = camera.scenePosition;
        _vertexData[0] = pos.x;
        _vertexData[1] = pos.y;
        _vertexData[2] = pos.z;
        _vertexData[4] = _vertexData[5] = _vertexData[6] = camera.lens.far/Math.sqrt(3);
        context.setProgramConstantsFromMatrix(Context3DProgramType.VERTEX, 0, viewProjection, true);
        context.setProgramConstantsFromVector(Context3DProgramType.VERTEX, 4, _vertexData, 2);
        renderable.activateVertexBuffer(0, stage3DProxy);
        context.drawTriangles(renderable.getIndexBuffer(stage3DProxy), 0, renderable.numTriangles);
}
 
/**
 * @inheritDoc
 */
override arcane function activate(stage3DProxy:Stage3DProxy, camera:Camera3D):void
{
        super.activate(stage3DProxy, camera);
        var context:Context3D = stage3DProxy._context3D;
        context.setDepthTest(false, Context3DCompareMode.LESS);
        context.setTextureAt(0, _cubeTexture.getTextureForStage3D(stage3DProxy));
}
```

Fagal上传数据代码

```javascript
override protected function initBuffers():void
{
        super.initBuffers();
        textureBuffer = new FSBuffer(Context3DBufferTypeID.TEXTURE_FS, updateTextureBuffer);
        projectionBuffer = new VCMatrixBuffer(Context3DBufferTypeID.PROJECTION_VC_MATRIX, updateProjectionBuffer);
        cameraPosBuffer = new VCVectorBuffer(Context3DBufferTypeID.CAMERAPOS_VC_VECTOR, updateCameraPosBuffer);
        scaleSkyboxBuffer = new VCVectorBuffer(Context3DBufferTypeID.SCALESKYBOX_VC_VECTOR, updateScaleSkyboxBuffer);
}
 
private function updateProjectionBuffer():void
{
        projectionBuffer.update(_modelViewProjection, true);
}
 
private function updateCameraPosBuffer():void
{
        cameraPosBuffer.update(cameraPos);
}
 
private function updateScaleSkyboxBuffer():void
{
        scaleSkyboxBuffer.update(scaleSkybox);
}
 
private function updateTextureBuffer():void
{
        textureBuffer.update(_cubeTexture);
}
 
override protected function updateDepthTestBuffer():void
{
        super.updateDepthTestBuffer();
 
        depthTestBuffer.update(false, Context3DCompareMode.LESS);
}
```

away3d上传数据时需要指定firstRegister编号，比如

```javascript
context.setProgramConstantsFromMatrix(Context3DProgramType.VERTEX, 0, viewProjection, true);
```

Fagal上传数据代码都是些什么啊，其实是看不到向Contex3d上传数据的过程的，因为这些Fagal已经为你做好了。上面的代码都是些向Fagal上传更新数据的过程。Fagal会根据（Context3DBufferTypeID.TEXTURE_FS == “texture_fs”) 对应Fagal函数Register元标签信息来自动指定firstRegister编号。因为不用手动自定firstRegister编号，所以代码会变得容易编写且不容易出错。