# 场景建设 模型设计

![](http://images.feng3d.me/wordpress/wp-content/uploads/2014/04/20140423233449-300x225.jpg)

[查看实例](http://images.feng3d.me/feng3dDemo/Test5_1.html)

从这个实例开始，我使用了资源加载的方式，查看实例的时候速度会比较快，但物体会将一个一个地显示出来。

就这个例子已经实现了从配置创建场景物体的功能。

config.xml中添加一个item,设置物体的位置，模型，贴图，缩放比例，朝向。

 

模型文件：ground.xml

贴图：groundbmp.jpg

这三个配合就可以轻松在场景中添加物体。

```
package pan
{
	import flash.display.BitmapData;
	import flash.display.Sprite;
	import flash.display.StageAlign;
	import flash.display.StageScaleMode;
	import flash.events.Event;
	import flash.events.MouseEvent;
	import flash.geom.Vector3D;
	import flash.utils.Dictionary;
	
	import me.feng.core.GlobalDispatcher;
	import me.feng.load.Load;
	import me.feng.load.LoadEvent;
	import me.feng3d.cameras.Camera3D;
	import me.feng3d.containers.ObjectContainer3D;
	import me.feng3d.containers.View3D;
	import me.feng3d.core.base.Geometry;
	import me.feng3d.core.base.subgeometry.SubGeometry;
	import me.feng3d.debug.Trident;
	import me.feng3d.entities.Mesh;
	import me.feng3d.materials.TextureMaterial;
	import me.feng3d.utils.Cast;
	
	import pan.data.PanParser;
 
	/**
	 * <1> 场景建设 模型设计
	 * @author warden_feng 2014-4-22
	 */
	[SWF(width = "640", height = "480", frameRate = "60", backgroundColor = "#FFFFFF")]
	public class Test5_1 extends Sprite
	{
		public var view:View3D;
 
		/** 相机旋转角度 */
		private var cameraAngle:Number = 0;
 
		/** 相机起始离物体的距离 */
		private var len:Number = 40;
 
		/** 世界 */
		private var world:ObjectContainer3D;
		
		/** 几何体字典 */
		private var geometryDic:Dictionary = new Dictionary();
		/** 材质字典 */
		private var materialDic:Dictionary = new Dictionary();
 
		private var dispatcher:GlobalDispatcher = GlobalDispatcher.instance;
		
		/** 配置文件地址 */
		private var configPath:String = "assets/config.xml";
 
		public function Test5_1()
		{
			MyCC.initFlashConsole(this);
			new ConsoleExtension();
			if (stage != null)
				init();
			else
				addEventListener(Event.ADDED_TO_STAGE, init);
		}
 
		private function init(e:Event = null):void
		{
			if (hasEventListener(Event.ADDED_TO_STAGE))
				removeEventListener(Event.ADDED_TO_STAGE, init);
 
			stage.scaleMode = StageScaleMode.NO_SCALE;
			stage.align = StageAlign.TOP_LEFT;
 
			view = new View3D();
			view.name = "mainView3D";
			addChild(view);
 
			view.scene.addChild(new Trident(1000));
 
			world = new ObjectContainer3D();
			world.name = "world";
			world.scale(20);
			view.scene.addChild(world);
 
			initMyCamera();
			initSmallMap();
 
			Load.init();
			loadConfig();
 
			addEventListener(Event.ENTER_FRAME, onEnterFrame);
			stage.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			stage.addEventListener(MouseEvent.MOUSE_WHEEL, onMouseWheel);
		}
 
		protected function onEnterFrame(event:Event):void
		{
 
		}
 
		/** 添加场景元件 */
		private function addElements(itemList:Vector.<Element>):void
		{
			var element:Element;
			for (var i:int = 0; i < itemList.length; i++)
			{
				element = itemList[i];
				element.geometryUrl = "assets/model/" + element.geometryName + ".xml";
				element.materialUrl = "assets/img/" + element.materialName + ".jpg";
 
				element.geometry ||= geometryDic[element.geometryUrl];
				element.material ||= materialDic[element.materialUrl];
 
				if (element.geometry && element.material)
				{
					createElement(element);
				}
				else
				{
					var loadObj:Object = {};
					loadObj.urls = [element.geometryUrl, element.materialUrl];
					loadObj.singleComplete = singleGeometryComplete;
					loadObj.singleCompleteParam = {element: element}
					loadObj.allItemsLoaded = allItemsLoaded;
					loadObj.allItemsLoadedParam = {element: element};
					dispatcher.dispatchEvent(new LoadEvent(LoadEvent.LOAD_RESOURCE, loadObj));
				}
			}
		}
 
		/** 设置照相机网格 */
		private function initMyCamera():void
		{
			var obj3d:Mesh = new Mesh();
			obj3d.name = "cameraMesh";
			obj3d.geometry = cameraGeometry;
			view.camera.addChild(obj3d);
 
			view.scene.addChild(view.camera);
 
			updateCamera();
		}
		
		/** 获取照相机几何结构 */
		public function get cameraGeometry():Geometry
		{
			var indexBuffer:Vector.<uint> = Vector.<uint>([0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23]);
			var positionsBuffer:Vector.<Number> = Vector.<Number>([0, 0, -25, -10, -10, 0, 10, -10, 0, 0, 0, -25, 10, -10, 0, 10, 10, 0, 0, 0, -25, 10, 10, 0, -10, 10, 0, 0, 0, -25, -10, 10, 0, -10, -10, 0, -10, -10, 0, 0, 0, 0, 10, -10, 0, 10, -10, 0, 0, 0, 0, 10, 10, 0, 10, 10, 0, 0, 0, 0, -10, 10, 0, -10, 10, 0, 0, 0, 0, -10, -10, 0]);
			var uvBuffer:Vector.<Number> = Vector.<Number>([0.5, 0, 0, 1, 1, 1, 0.5, 0, 0, 1, 1, 1, 0.5, 0, 0, 1, 1, 1, 0.5, 0, 0, 1, 1, 1, 0, 0, 0.5, 0.5, 1, 0, 1, 0, 0.5, 0.5, 1, 1, 1, 1, 0.5, 0.5, 0, 1, 0, 1, 0.5, 0.5, 0, 0]);
			
			var geometry:Geometry = new Geometry();
			var subGeometry:SubGeometry = new SubGeometry();
			subGeometry.updateIndexData(indexBuffer);
			subGeometry.updateVertexData(positionsBuffer);
			subGeometry.updateUVData(uvBuffer);
			geometry.addSubGeometry(subGeometry);
			
			return geometry;
		}
 
		/** 初始化右上角小地图 */
		private function initSmallMap():void
		{
			//俯视图
			var topCamera:Camera3D = new Camera3D();
			topCamera.name = "topCamera";
			topCamera.y = 50;
			topCamera.lookAt(new Vector3D(), new Vector3D(0, 0, 1));
 
			var topRightView:View3D = new View3D(view.scene, topCamera);
			topRightView.name = "smallView3D";
			addChild(topRightView);
			topRightView.width = topRightView.height = 100;
			topRightView.x = stage.stageWidth - topRightView.width;
			topRightView.backgroundColor = 0x666666;
		}
 
		protected function onMouseMove(event:MouseEvent):void
		{
			cameraAngle = int(event.stageX * 360 / stage.stageWidth);
 
			updateCamera();
		}
 
		protected function onMouseWheel(event:MouseEvent):void
		{
			if (event.delta > 0)
			{
				len *= 1.2;
			}
			else
			{
				len /= 1.2;
			}
			updateCamera();
		}
 
		/** 更新照相机位置角度 */
		private function updateCamera():void
		{
			view.camera.z = len * Math.cos(cameraAngle / 180 * Math.PI);
			view.camera.x = len * Math.sin(cameraAngle / 180 * Math.PI);
			view.camera.y = len;
 
			view.camera.lookAt(new Vector3D());
		}
 
		/** 加载配置 */
		private function loadConfig():void
		{
			dispatcher.dispatchEvent(new LoadEvent(LoadEvent.LOAD_RESOURCE, {urls: [configPath], singleComplete: configLoadCompleted, singleCompleteParam: {}}));
		}
 
		/** 一个场景对象的所有资源加载完毕 */
		private function allItemsLoaded(param:Object):void
		{
			var element:Element = param.element;
			element.geometry ||= geometryDic[element.geometryUrl];
			element.material ||= materialDic[element.materialUrl];
 
			if (element.geometry && element.material)
			{
				createElement(element);
			}
			else
			{
				trace("创建" + element.name + "失败！");
			}
		}
 
		/** 单个模型数据加载完毕 */
		private function singleGeometryComplete(param:Object):void
		{
			var element:Element = param.element;
			if (param.url == element.geometryUrl)
			{
				var geometry:Geometry = geometryDic[param.url];
				if (geometry)
					return;
				var config:XML = Load.loader.getXML(param.url);
				config.ignoreWhitespace = true;
 
				var _groundStringV:String = config.v;
				var _groundStringU:String = config.u;
				var _groundStringUV:String = config.uv;
				geometryDic[param.url] = PanParser.getPanGeometry(_groundStringV, _groundStringU, _groundStringUV, 0.002);
			}
			else if (param.url == element.materialUrl)
			{
				var bitmapdata:BitmapData = Load.loader.getBitmapData(param.url);
				materialDic[param.url] = new TextureMaterial(Cast.bitmapTexture(bitmapdata));
			}
 
		}
 
		/** 配置文件加载完毕 */
		private function configLoadCompleted(param:Object):void
		{
			if (configPath == param.url)
			{
				var config:XML = Load.loader.getXML(configPath);
 
				config.ignoreWhitespace = true;
				var itemList:Vector.<Element> = new Vector.<Element>();
				for each (var j:* in config.item)
				{
					itemList.push(new Element(j));
				}
			}
			addElements(itemList);
		}
 
		/** 创建场景对象 */
		private function createElement(element:Element):void
		{
			var mesh:Mesh = new Mesh(element.geometry, element.material);
			mesh.name = element.name;
			mesh.x = element.x;
			mesh.y = element.y;
			mesh.z = element.z;
			mesh.scale(element.scale);
			mesh.rotationY = element.rotationY;
			world.addChild(mesh);
		}
	}
}
import me.feng3d.core.base.Geometry;
import me.feng3d.materials.MaterialBase;
 
class Element
{
	public var name:String;
	public var x:Number;
	public var y:Number;
	public var z:Number;
	public var scale:Number;
	public var rotationY:Number;
	public var geometryName:String;
	public var materialName:String;
 
	public var geometry:Geometry;
	public var material:MaterialBase;
 
	public var geometryUrl:String;
	public var materialUrl:String;
 
	public function Element(j:*)
	{
		name = j.@name;
		x = j.@x;
		y = j.@y;
		z = j.@z;
		scale = j.@scale;
		rotationY = j.@rotationY;
		geometryName = j.@geometry;
		materialName = j.@material;
	}
}
```